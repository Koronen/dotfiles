# vim: set ft=zsh:

prompt_koronen_pwd () {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_koronen_pwd="$MATCH"
    unset MATCH
  else
    _prompt_koronen_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

prompt_koronen_host () {
  if [[ -n "$SSH_CONNECTION" ]]; then
    _prompt_koronen_host="%m"
  else
    _prompt_koronen_host=""
  fi
}

prompt_koronen_precmd () {
  prompt_koronen_pwd
  prompt_koronen_host
  vcs_info
}

prompt_koronen_setup () {
  prompt_opts=(percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_koronen_precmd

  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' formats '%s:%b%c%u%m '
  zstyle ':vcs_info:*' actionformats "%s:%b%c%u:%a%m "

  zstyle ':vcs_info:(hg*|git*):*' get-revision true
  zstyle ':vcs_info:(hg*|git*):*' check-for-changes true

  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' unstagedstr '!'
  zstyle ':vcs_info:git*+set-message:*' hooks git-stash git-untracked

  zstyle ':vcs_info:hg*' branchformat "%b"

  PROMPT='${_prompt_koronen_host}%(!.#.$) '
  RPROMPT='%(?::%F{red}ret:$?%f )%T ${vcs_info_msg_0_}${_prompt_koronen_pwd}'
}

+vi-git-untracked () {
  if command git status --porcelain | grep -qF "??"; then
    hook_com[unstaged]+='?'
  fi
}

+vi-git-stash () {
  if [[ -s ${hook_com[base]}/.git/refs/stash ]]; then
    hook_com[misc]+="@"
  fi
}

prompt_koronen_setup "$@"
